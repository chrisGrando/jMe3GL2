allprojects {

    group = 'org.je3gl'
    version = '3.0.1'

    project.ext {
        // global dependencies
        jmonkeyengineVersion = '3.8.1-stable'
        dyn4jVersion = '5.0.2'
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }

        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
    }

    tasks.named('test') {
        useJUnitPlatform() // Use JUnit Platform for unit tests.
    }

    tasks.register('copyPomFileToLibsFolder', Copy) {
        dependsOn 'generatePomFileForMavenJavaPublication'
        from "${buildDir}/publications/mavenJava/pom-default.xml"
        into "${buildDir}/libs"
        rename 'pom-default.xml', project.name + '-' + version + '.pom'
    }

    build.finalizedBy('copyPomFileToLibsFolder')

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version
                from components.java

                pom {
                    url = 'https://github.com/chrisGrando/jMe3GL2'
                    licenses {
                        license {
                            name = 'The 3-Clause BSD License'
                            url = 'https://opensource.org/license/bsd-3-clause/'
                        }
                    }
                }
            }
        }
    }
}
